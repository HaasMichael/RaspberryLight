apply plugin: 'java'
apply plugin: 'maven'

String mavenGroupId = 'RaspberryLight'
String mavenVersion = '1.0-SNAPSHOT'
String springBootRelease = '1.1.8.RELEASE'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "http://repo.spring.io/libs-release" }
    maven { url "http://repository.jboss.org/nexus/content/groups/public/" }
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-parent', version: springBootRelease

    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootRelease
}

String mavenArtifactId = name

group = mavenGroupId
version = mavenVersion

task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates a jar from the source files.') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

configure(install.repositories.mavenInstaller) {
    pom.project {
        groupId = mavenGroupId
        artifactId = mavenArtifactId
        version = mavenVersion
    }
}

task createFolders(description: 'Creates the source folders if they do not exist.') doLast {
    sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
            println "Creating source folder: ${srcDir}"
            srcDir.mkdirs()
        }
    }
}
